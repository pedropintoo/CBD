// NMEC: 115304

# 1. Encontre todos os atores que dirigiram um filme em que também atuaram e apresente o nome do ator e o título do filme.

```sql
match (actor:Person)-[:DIRECTED]->(filme:Movie)<-[:ACTED_IN]-(actor:Person)
return actor.name, filme.title
```

# 2. Para cada filme realizado depois de 2005, apresente os nomes de todos os atores que atuaram nesse filme.

```sql
match (actor:Person)-[:ACTED_IN]->(movie:Movie)
where movie.released > 2005
return movie.title, collect(actor.name)
```

# 3. Encontre pares de nós com mais do que uma relação entre si.

```sql
match (a)-[r]->(b)
with a, b, count(r) as count
where count > 1
return a, b, count
```

# 4. Encontre todos os pares de pessoas que fizeram revisões do mesmo filme. Apresente os seus nomes e título de cada filme.

```sql
match (p1:Person)-[:REVIEWED]->(movie:Movie)<-[:REVIEWED]-(p2:Person)
WHERE p1.name < p2.name
return movie.title, p1.name, p2.name
```

# 5. Encontre todos os pares de atores que atuaram em vários filmes juntos.

```sql
match (a1:Person)-[:ACTED_IN]->(:Movie)<-[:ACTED_IN]-(a2:Person)
with a1, a2, count(\*) as c
where c > 1 and a1.name < a2.name
return a1.name, a2.name
```

# 6. Determine a idade média do elenco do filme "Apollo 13" no ano do lançamento do filme.

```sql
match (movie:Movie {title: "Apollo 13"})<-[:ACTED_IN]-(actor:Person)
return movie.title, round(avg(movie.released - actor.born), 2)
```

# 7. Encontre os 10 filmes com o elenco mais velho no momento do lançamento do filme. Apresente o filme e a idade média arredondada a 2 casas decimais, por ordem decrescente.

```sql
match (movie:Movie)<-[:ACTED_IN]-(actor:Person)
with movie, movie.released - actor.born as age
order by age desc
return movie.title, round(avg(age), 2)
limit 10
```

# 8. Apresente o subgrafo ACTED_IN do filme com o elenco mais novo, no momento do lançamento do filme.

```sql
match (actor:Person)-[r:ACTED_IN]->(movie:Movie)
with movie, avg(movie.released - actor.born) as avg_age
order by avg_age
limit 1
match (actor:Person)-[r:ACTED_IN]->(movie)
return actor, r, movie
```

# 9. Qual é o caminho mais curto (usando qualquer tipo de relação) entre John Cusack e Demi Moore?

```sql
match p = shortestPath((a:Person {name: "John Cusack"})-[*]-(b:Person {name: "Demi Moore"}))
RETURN p
```

# 10. Qual a dimensão caminho mais curto (usando qualquer tipo de relação) entre Keanu Reeves e Tom Cruise?

```sql
match p = shortestPath((a:Person {name: "Keanu Reeves"})-[*]-(b:Person {name: "Tom Cruise"}))
RETURN length(p)
```

# 11. Quais são as dimensões do caminho mais curto entre pessoas com nome Jim e pessoas com nome Kevin?

```sql
match p = shortestPath((p1:Person)-[*]-(p2:Person))
where p1 <> p2 and p1.name contains "Jim" and p2.name contains "Kevin"
return length(p)
```

# 12. Que pessoas têm uma distância 2 para Jim Cash (a distância entre duas pessoas é o comprimento do caminho mais curto entre eles)?

```sql
match (p1:Person {name: "Jim Cash"}), (p2:Person)
where p1 <> p2
match p = shortestPath((p1)-[*..2]-(p2))
where length(p) = 2
return p2.name
```

# 13. Qual é a maior distância de uma pessoa para Kevin Bacon?

```sql
match p = shortestPath((p1:Person {name: "Kevin Bacon"})-[*]-(p2:Person))
where p1 <> p2
return max(length(p))
```

# 14. Qual é a maior distância entre duas pessoas?

```sql
match p = shortestPath((p1:Person)-[*]-(p2:Person))
where p1 <> p2
return max(length(p))
```

# 15. Qual é a distribuição de distâncias em pares (isto é, para a distância 1, 2, 3, ..., quantos pares de pessoas têm essa distância um do outro)?

```sql
match p = shortestPath((p1:Person)-[*]-(p2:Person))
where p1 <> p2
return length(p), count(\*)
order by length(p)
```

# 16. Indique as 10 pessoas com menor distância média em que o caminho entre elas são relações do tipo ACTED_IN.

```sql
match p = shortestPath((p1:Person)-[:ACTED_IN*]-(p2:Person))
where p1 <> p2
with p1, p2, length(p) as size
return p1.name, avg(size) as avg_len
order by avg_len
limit 10
```

# 17. Apresente os atores que tiveram pelo menos uma role para personagens com o prefixo 'Dr.'. O resultado deve ser uma lista com o par “Ator, Lista das roles”.

```sql
match (actor:Person)-[r:ACTED_IN]->(movie:Movie)
with actor, FILTER(role IN r.roles WHERE role STARTS WITH "Dr.") AS roles
where size(roles) > 0
return actor.name, roles
```

# 18. Apresente todos os autores de filmes com tagline com story.
```sql
match (movie:Movie)<-[:ACTED_IN]-(actor:Person)
where movie.tagline contains "story"
return actor.name, movie.title
```

# 19. A pessoa que dirigiu mais filmes.
```sql
match (director:Person)-[:DIRECTED]->(movie:Movie)
return director.name, count(movie) as count
order by count desc
limit 1
```

# 20. Apresenta os atores que participaram em filmes distintos com a mesma role.
```sql
match (actor:Person)-[r1:ACTED_IN]->(movie1:Movie), (actor)-[r2:ACTED_IN]->(movie2:Movie)
where r1.roles = r2.roles and id(movie1) < id(movie2)
return actor.name, movie1.title, movie2.title, r1.roles
```
