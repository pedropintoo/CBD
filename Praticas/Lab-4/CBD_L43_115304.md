// NMEC: 115304

## 0. Carregue o conteudo do ficheiro `/resources/git_selection.csv`.

#### Explicações
1. Criação de nós para utilizadores, projetos e organizações.
2. Criação de relações entre utilizadores e projetos. (com atributos)
3. Criação de relações entre utilizadores e organizações.


#### Para evitar duplicação:

1. Utilizadores são identificados pelo nome.
   - Podendo ter vários `svn_id`.
2. Projetos são identificados pelo nome.
3. Contribuições (CONTRIBUTES_IN) conectam utilizadores e projetos
   - Contribuições podem ter várias `role_on_project`
4. Organizações são identificadas pelo nome.
5. Trabalhar para (WORKS_ON) conectam utilizador e organizações

```sql
LOAD CSV WITH HEADERS FROM 'file:///resources/git_selection.csv' AS row
with row,
    toLower(trim(row.svn_id)) AS userId,
    REDUCE(
    s = toUpper(left(toLower(head(split(trim(row.real_name), ' '))), 1)) + substring(toLower(head(split(trim(row.real_name), ' '))), 1),
    w IN tail(split(trim(row.real_name), ' ')) | s + ' ' + toUpper(left(toLower(w), 1)) + substring(toLower(w), 1)
    ) AS camelUserName,
    REDUCE(
    s = toUpper(left(toLower(head(split(trim(row.project_name), ' '))), 1)) + substring(toLower(head(split(trim(row.project_name), ' '))), 1),
    w IN tail(split(trim(row.project_name), ' ')) | s + ' ' + toUpper(left(w,1)) + substring(w,1)
    ) AS camelProjectName,
    REDUCE(
    s = toUpper(left(toLower(head(split(trim(row.organization), ' '))), 1)) + substring(toLower(head(split(trim(row.organization), ' '))), 1),
    w IN tail(split(trim(row.organization), ' ')) | s + ' ' + toUpper(left(w,1)) + substring(w,1)
    ) AS camelOrgName,
    REDUCE(
    s = toUpper(left(toLower(head(split(trim(row.role_on_project), ' '))), 1)) + substring(toLower(head(split(trim(row.role_on_project), ' '))), 1),
    w IN tail(split(trim(row.role_on_project), ' ')) | s + ' ' + toUpper(left(w,1)) + substring(w,1)
    ) AS camelRoleOnProject

MERGE (u:User {name: camelUserName})
ON CREATE SET u.svn_ids = [userId]
ON MATCH SET u.svn_ids = CASE WHEN userId IN u.svn_ids THEN u.svn_ids ELSE u.svn_ids + userId END

MERGE (p:Project {name: camelProjectName})

MERGE (u)-[c:CONTRIBUTES_IN]->(p)
ON CREATE SET c.role_on_project = [camelRoleOnProject]
ON MATCH SET c.role_on_project = CASE WHEN camelRoleOnProject IN c.role_on_project THEN c.role_on_project ELSE c.role_on_project + camelRoleOnProject END

MERGE (o:Organization {name: camelOrgName})

MERGE (u)-[:WORKS_ON]->(o);
```

## 1. Liste a informação de cada utilizador.
```sql
match (u:User) 
return u
```

## 2. Liste o nome de cada utilizador.
```sql
match (u:User)
return u.name
```

## 3. Liste a informação de cada projeto, no qual tenha participado pelo menos um utilizador.
```sql
match (p:Project)<-[:CONTRIBUTES_IN]-(u:User)
return distinct p
```

## 4. Liste os utilizadores e total de projetos em que cada um colabora.
```sql
match (u:User)-[:CONTRIBUTES_IN]->(p:Project)
return u.name, count(p)
```

## 5. Liste os utilizadores e do total de projetos em que cada colabora ordenados por ordem decrescente do total.
```sql
match (u:User)-[:CONTRIBUTES_IN]->(p:Project)
return u.name, count(p)
order by count(p) desc
```

## 6. Liste projetos e total de membros em cada projeto.
```sql
match (p:Project)<-[:CONTRIBUTES_IN]-(u:User)
return p.name, count(u)
```

## 7. Liste projetos e total de membros com a role "Committer" em cada projeto.
```sql
match (p:Project)<-[r:CONTRIBUTES_IN]-(u:User)
where "Committer" in r.role_on_project
return p.name, count(u)
```

## 8. Liste todos os utilizadores que participaram nos mesmo projetos do que o utilizador "atm" (id). Mostre os atributos: nome de "atm", nome utilizador 2, nome do projeto.
```sql
match (u1:User)-[:CONTRIBUTES_IN]->(p:Project)<-[:CONTRIBUTES_IN]-(u2:User)
where u1 <> u2 and "atm" in u1.svn_ids
return u1.name, u2.name, p.name
```

## 9. Liste todos os utilizadores que participaram com a role "Committer" em projetos em que o utilizador "atm" (id1) participou com a role "PMC".
```sql
match (u1:User)-[c1:CONTRIBUTES_IN]->(p:Project)<-[c2:CONTRIBUTES_IN]-(u2:User)
where u1 <> u2 and "atm" in u1.svn_ids and "Pmc" in c1.role_on_project and "Committer" in c2.role_on_project
return u2.name
```
#### Nota: "PMC" foi convertido para "Pmc" para evitar duplicação (case insensitive). Definido na modelação.

